D=13
A#=0:B#=0:C#=0
USEGRAPHIC
DIM P#(30),Q#(30)
RESTORE DATA1
N=0
WHILE N<=30
 P#(N)=FLOAT#(READ())
 Q#(N)=FLOAT#(READ())
 IF P#(N)<0 THEN N=N-1:BREAK
 N=N+1
WEND

LABEL JUMP1
WHILE 1
GCOLOR 4:GOSUB DRAW
WAIT 10
GCOLOR 0:GOSUB DRAW
K=KEYS()
IF K AND 1 THEN A#=A#+0.1
IF K AND 2 THEN A#=A#-0.1
IF K AND 4 THEN B#=B#+0.1
IF K AND 8 THEN B#=B#-0.1
IF K AND 16 THEN C#=C#+0.1
IF K AND 32 THEN C#=C#-0.1
WEND

LABEL DRAW
VAR I,J
VAR U#,V#,W#,X#,Y#,Z#
FOR J=0 TO D
F#=FLOAT#(J)/FLOAT#(D)*2*PI#
FOR I=0 TO N
X#=P#(I)*COS#(F#)
Y#=Q#(I)
Z#=P#(I)*SIN#(F#)

U#=X#
V#=GOSUB#(ROTATX,Y#,Z#,A#)
W#=GOSUB#(ROTATY,Y#,Z#,A#)

Y#=V#
Z#=GOSUB#(ROTATX,W#,U#,B#)
X#=GOSUB#(ROTATY,W#,U#,B#)

W#=Z#
U#=GOSUB#(ROTATX,X#,Y#,C#)
V#=GOSUB#(ROTATY,X#,Y#,C#)

IF I=0 THEN
 POINT INT(U#)+128,INT(V#)+112
ELSE
 LINE ,INT(U#)+128,INT(V#)+112
ENDIF
NEXT
NEXT

FOR I=0 TO N
FOR J=0 TO D
F#=FLOAT#(J)/FLOAT#(D)*2*PI#

X#=P#(I)*COS#(F#)
Y#=Q#(I)
Z#=P#(I)*SIN#(F#)

U#=X#
V#=GOSUB#(ROTATX,Y#,Z#,A#)
W#=GOSUB#(ROTATY,Y#,Z#,A#)

Y#=V#
Z#=GOSUB#(ROTATX,W#,U#,B#)
X#=GOSUB#(ROTATY,W#,U#,B#)

W#=Z#
U#=GOSUB#(ROTATX,X#,Y#,C#)
V#=GOSUB#(ROTATY,X#,Y#,C#)

IF J=0 THEN
 POINT INT(U#)+128,INT(V#)+112
ELSE
 LINE ,INT(U#)+128,INT(V#)+112
ENDIF
NEXT
NEXT
RETURN

LABEL ROTATX
R#=ARGS#(1):S#=ARGS#(2):T#=ARGS#(3)
RETURN R#*COS#(T#)-S#*SIN#(T#)
LABEL ROTATY
R#=ARGS#(1):S#=ARGS#(2):T#=ARGS#(3)
RETURN R#*SIN#(T#)+S#*COS#(T#)
LABEL DATA1
DATA 55,-85,60,-50,60,-30,
 40,0,5,20,5,60,40,70,
 40,80,0,80,
 -1,0
